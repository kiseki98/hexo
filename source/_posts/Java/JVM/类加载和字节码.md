---
title: 类加载和字节码
date: 2022/5/19 11:46:25
tags:
- Java
- JVM
- 类加载
categories:
- [Java, JVM]
description: 类加载和字节码
---

# 类文件结构

![批注 2020-04-04 103406](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-103406.png)

```java
// HelloWorld程序编码后的二进制文件如下（16进制展现，2位表示一个字节）
// [root@violet home]# od -t xC HelloWorld.class
// 魔数：ca fe ba be（0-3）表示它是否是class类型的文件
// 版本：00 00 00 34（4-7，前两个小版本，后两个大版本，00 34（52） 表示是java8）
// 常量池长度：00 1f(31)表示常量池有#1-#30项
// 第#1项0a（10）表示一个Method信息，00 06和00 11（17）表示他引用了常量池中#6和#17项来获得这个方法的【所属类】和【方法名】。
// 第#2项09表示一个Filed信息，00 16(22)和00 17(23) 表示它引用了常量池中#22和#23来获得这个成员变量的【所属类】和【成员变量名】
// 黄色高亮表示一项，每一项后面四个字节（总计#30项）
// 访问标志和继承信息：常量池第一项往后算到#30项，之后就是
0000000 ca fe ba be 00 00 00 34 00 1f 0a 00 06 00 11 09
0000020 00 12 00 13 08 00 14 0a 00 15 00 16 07 00 17 07
0000040 00 18 01 00 06 3c 69 6e 69 74 3e 01 00 03 28 29
0000060 56 01 00 04 43 6f 64 65 01 00 0f 4c 69 6e 65 4e
0000100 75 6d 62 65 72 54 61 62 6c 65 01 00 04 6d 61 69
0000120 6e 01 00 16 28 5b 4c 6a 61 76 61 2f 6c 61 6e 67
0000140 2f 53 74 72 69 6e 67 3b 29 56 01 00 10 4d 65 74
0000160 68 6f 64 50 61 72 61 6d 65 74 65 72 73 01 00 04
0000200 61 72 67 73 01 00 0a 53 6f 75 72 63 65 46 69 6c
0000220 65 01 00 0f 48 65 6c 6c 6f 57 6f 72 6c 64 2e 6a
0000240 61 76 61 0c 00 07 00 08 07 00 19 0c 00 1a 00 1b
0000260 01 00 0b 48 65 6c 6c 6f 20 57 6f 72 6c 64 07 00
0000300 1c 0c 00 1d 00 1e 01 00 0a 48 65 6c 6c 6f 57 6f
0000320 72 6c 64 01 00 10 6a 61 76 61 2f 6c 61 6e 67 2f
0000340 4f 62 6a 65 63 74 01 00 10 6a 61 76 61 2f 6c 61
0000360 6e 67 2f 53 79 73 74 65 6d 01 00 03 6f 75 74 01
0000400 00 15 4c 6a 61 76 61 2f 69 6f 2f 50 72 69 6e 74
0000420 53 74 72 65 61 6d 3b 01 00 13 6a 61 76 61 2f 69
0000440 6f 2f 50 72 69 6e 74 53 74 72 65 61 6d 01 00 07
0000460 70 72 69 6e 74 6c 6e 01 00 15 28 4c 6a 61 76 61
0000500 2f 6c 61 6e 67 2f 53 74 72 69 6e 67 3b 29 56 00
0000520 21 00 05 00 06 00 00 00 00 00 02 00 01 00 07 00
0000540 08 00 01 00 09 00 00 00 1d 00 01 00 01 00 00 00
0000560 05 2a b7 00 01 b1 00 00 00 01 00 0a 00 00 00 06
0000600 00 01 00 00 00 01 00 09 00 0b 00 0c 00 02 00 09
0000620 00 00 00 25 00 02 00 01 00 00 00 09 b2 00 02 12
0000640 03 b6 00 04 b1 00 00 00 01 00 0a 00 00 00 0a 00
0000660 02 00 00 00 03 00 08 00 04 00 0d 00 00 00 05 01
0000700 00 0e 00 00 00 01 00 0f 00 00 00 02 00 10
0000716               
```

# 图解运行流程

## 原始代码

```java
public class Demo{
    public static void main(String[] args) {
        int a=10;
        int b=Short.MAX_VALUE+1;
        int c=a+b;
        System.out.println(c);
    }
}
```

```java
// 反编译后：javap -v 字节码文件
// F:\Demo\springproject\src\main\java\com\example\springproject>javap -v Demo.class
Classfile /F:/Demo/springproject/src/main/java/com/example/springproject/Demo.class
  Last modified 2021-5-26; size 452 bytes
  MD5 checksum 956a20f518476f44ee34b47cf20cc8f4
  Compiled from "Demo.java"
public class com.example.springproject.Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #7.#16         // java/lang/Object."<init>":()V
   #2 = Class              #17            // java/lang/Short
   #3 = Integer            32768
   #4 = Fieldref           #18.#19        // java/lang/System.out:Ljava/io/PrintStream;
   #5 = Methodref          #20.#21        // java/io/PrintStream.println:(I)V
   #6 = Class              #22            // com/example/springproject/Demo
   #7 = Class              #23            // java/lang/Object
   #8 = Utf8               <init>
   #9 = Utf8               ()V
  #10 = Utf8               Code
  #11 = Utf8               LineNumberTable
  #12 = Utf8               main
  #13 = Utf8               ([Ljava/lang/String;)V
  #14 = Utf8               SourceFile
  #15 = Utf8               Demo.java
  #16 = NameAndType        #8:#9          // "<init>":()V
  #17 = Utf8               java/lang/Short
  #18 = Class              #24            // java/lang/System
  #19 = NameAndType        #25:#26        // out:Ljava/io/PrintStream;
  #20 = Class              #27            // java/io/PrintStream
  #21 = NameAndType        #28:#29        // println:(I)V
  #22 = Utf8               com/example/springproject/Demo
  #23 = Utf8               java/lang/Object
  #24 = Utf8               java/lang/System
  #25 = Utf8               out
  #26 = Utf8               Ljava/io/PrintStream;
  #27 = Utf8               java/io/PrintStream
  #28 = Utf8               println
  #29 = Utf8               (I)V
{
  public com.example.springproject.Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1		 // 操作数栈最大深度，局部变量表长度
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         0: bipush        10
         2: istore_1
         3: ldc           #3                  // int 32768
         5: istore_2
         6: iload_1
         7: iload_2
         8: iadd
         9: istore_3
        10: getstatic     #4                  // Field java/lang/System.out:Ljava/io/PrintStream;
        13: iload_3
        14: invokevirtual #5                  // Method java/io/PrintStream.println:(I)V
        17: return
      LineNumberTable:
        line 5: 0
        line 6: 3
        line 7: 6
        line 8: 10
        line 9: 17
}
SourceFile: "Demo.java"   
```

## 运行流程

### bipush 10

1. 将一个`byte`压入操作数栈(其长度会补齐4个字节)，类似的指令还有`sipush`将一 个`short`压入操作数栈(长度会补齐4个字节)
2. `ldc`将一 个`int`压入操作数栈
3. `ldc2_ w`将一个`long`压入操作数栈(分两次压入，因为`long`是8个字节)
4. 这里小的数字都是和字节码指令存在一起，超过 `short` 范围的数字存入了常量池
5. 操作数栈宽度4个字节，运算和赋值在操作数栈中进行

![批注 2020-04-04 161155](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-161155.png)

![批注 2020-04-04 162258](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162258.png)

### istore_1

将操作数栈顶数据弹出，存入局部变量表的`slot 1`（1号槽位）

![批注 2020-04-04 162320](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162320.png)

![批注 2020-04-04 162352](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162352.png)

### ldc	#3

1. 从常量池加载#3数据到操作数栈
2. 注意`Short.Max_Value`是32767，所以`32768=Short.Max_Value+1`实际是编译期间算好的(常量折叠优化)

![批注 2020-04-04 162641](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162641.png)

### istore_2

将操作数栈顶数据弹出，存入局部变量表的`slot 1`

![批注 2020-04-04 162809](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162809.png)

![批注 2020-04-04 162904](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-162904.png)

### iload_1

局部变量表1号位加载进操作数栈

![批注 2020-04-04 165409](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-165409.png)

### iload_2

局部变量表2号位加载进操作数栈

![批注 2020-04-04 165425](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-165425.png)

### iadd

1. `a+b`操作必须在操作数栈中进行，不能在局部变量表中进行
2. `add`弹出操作数栈中的两个局部变量，并且把计算结果存入操作数栈

![批注 2020-04-04 165501](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-165501.png)

![批注 2020-04-04 165546](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-165546.png)

### istore_3

把操作数栈中的结果存入局部变量表中

![批注 2020-04-04 170452](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-170452.png)

![批注 2020-04-04 170514](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-170514.png)

### getstatic #4   

把 `system.out` 的引用放入操作数栈中

![批注 2020-04-04 170704](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-170704.png)

### iload_3

加载局部变量表3号位进入操作数栈

![Image](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/Image-1622021116961.png)

![Image](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/Image-1622021123194.png)

### invokevirtual #5   

1. 找到常量池#5项
2. 定位方法区 `java/io/printstream.println:(i)V`方法
3. 生成新的栈帧（分配`locals`，`stack`等）
4. 传递参数，执行新栈帧中的字节码
5. 执行完毕，弹出栈帧
6. 清除`main`操作数栈内容

![Image](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/Image-1622021327178.png)

![Image](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/Image-1622021332188.png)

### return

1. 完成 `mian` 方法的调用
2. 程序结束

## 不同程序示例

### 赋值操作

```java
public class Demo {
    public static void main(String[] args) {
        int a = 0;
        if (a == 0) {
            a = 10;
        } else {
            a = 20;
        }
    }
}
```

```java
// 使用javap -v命令
// F:\Demo\springproject\src\main\java\com\example\springproject>javap -v Demo.class
Classfile /F:/Demo/springproject/src/main/java/com/example/springproject/Demo.class
  Last modified 2021-5-26; size 339 bytes
  MD5 checksum 36400b74f0bc74a8bc4818a4c45c76e1
  Compiled from "Demo.java"
public class com.example.springproject.Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #3.#13         // java/lang/Object."<init>":()V
   #2 = Class              #14            // com/example/springproject/Demo
   #3 = Class              #15            // java/lang/Object
   #4 = Utf8               <init>
   #5 = Utf8               ()V
   #6 = Utf8               Code
   #7 = Utf8               LineNumberTable
   #8 = Utf8               main
   #9 = Utf8               ([Ljava/lang/String;)V
  #10 = Utf8               StackMapTable
  #11 = Utf8               SourceFile
  #12 = Utf8               Demo.java
  #13 = NameAndType        #4:#5          // "<init>":()V
  #14 = Utf8               com/example/springproject/Demo
  #15 = Utf8               java/lang/Object
{
  public com.example.springproject.Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=2, args_size=1
         0: iconst_0
         1: istore_1
         2: iload_1
         3: ifne          12
         6: bipush        10
         8: istore_1
         9: goto          15
        12: bipush        20
        14: istore_1
        15: return
      LineNumberTable:
        line 5: 0
        line 6: 2
        line 7: 6
        line 9: 12
        line 11: 15
      StackMapTable: number_of_entries = 2
        frame_type = 252 /* append */
          offset_delta = 12
          locals = [ int ]
        frame_type = 2 /* same */
}
SourceFile: "Demo.java"
```

### 自增操作

```java
public class Demo {
    public static void main(String[] args) {
        int a = 0;
        while (a<10){
            a++;
        }
    }
}
```

```java
F:\Demo\springproject\src\main\java\com\example\springproject>javap -v Demo.class
Classfile /F:/Demo/springproject/src/main/java/com/example/springproject/Demo.class
  Last modified 2021-5-26; size 334 bytes
  MD5 checksum df1f6f3063facc1f612b82a7e30154a0
  Compiled from "Demo.java"
public class com.example.springproject.Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #3.#13         // java/lang/Object."<init>":()V
   #2 = Class              #14            // com/example/springproject/Demo
   #3 = Class              #15            // java/lang/Object
   #4 = Utf8               <init>
   #5 = Utf8               ()V
   #6 = Utf8               Code
   #7 = Utf8               LineNumberTable
   #8 = Utf8               main
   #9 = Utf8               ([Ljava/lang/String;)V
  #10 = Utf8               StackMapTable
  #11 = Utf8               SourceFile
  #12 = Utf8               Demo.java
  #13 = NameAndType        #4:#5          // "<init>":()V
  #14 = Utf8               com/example/springproject/Demo
  #15 = Utf8               java/lang/Object
{
  public com.example.springproject.Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=1
         0: iconst_0
         1: istore_1					   // 存的是0
         2: iload_1
         3: bipush        10
         5: if_icmpge     14
         8: iinc          1, 1 				// 局部变量表上面变量自增1，第一个表示下表，第二个表示自增多少
        11: goto          2
        14: return
      LineNumberTable:
        line 5: 0
        line 6: 2
        line 7: 8
        line 9: 14
      StackMapTable: number_of_entries = 2
        frame_type = 252 /* append */
          offset_delta = 2
          locals = [ int ]
        frame_type = 11 /* same */
}
SourceFile: "Demo.java"
```

# 构造方法学习

## cinit针对静态成员

编译器会按自上而下的顺序，收集所有的静态代码块(类加载**初始化**阶段执行)和静态成员变量赋值代码，合并位一个特殊的`<cinit>()V`，就是说所有赋值代码按顺序执行

```java
public class Demo {
    static int a = 10;
    static {
        a=20;
    }
    static {
        a=30;
    }
    public static void main(String[] args) {
        System.out.println(a);//结果30
    }
}
```

```java
// D:\IdeaProject\src>javap -v Demo.class
Classfile /D:/IdeaProject/src/Demo.class
  Last modified 2021-5-26; size 483 bytes
  MD5 checksum 41f530a27f67752c8a5b4fb86eabddcb
  Compiled from "Demo.java"
public class Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #6.#18         // java/lang/Object."<init>":()V
   #2 = Fieldref           #19.#20        // java/lang/System.out:Ljava/io/PrintStream;
   #3 = Fieldref           #5.#21         // Demo.a:I
   #4 = Methodref          #22.#23        // java/io/PrintStream.println:(I)V
   #5 = Class              #24            // Demo
   #6 = Class              #25            // java/lang/Object
   #7 = Utf8               a
   #8 = Utf8               I
   #9 = Utf8               <init>
  #10 = Utf8               ()V
  #11 = Utf8               Code
  #12 = Utf8               LineNumberTable
  #13 = Utf8               main
  #14 = Utf8               ([Ljava/lang/String;)V
  #15 = Utf8               <clinit>
  #16 = Utf8               SourceFile
  #17 = Utf8               Demo.java
  #18 = NameAndType        #9:#10         // "<init>":()V
  #19 = Class              #26            // java/lang/System
  #20 = NameAndType        #27:#28        // out:Ljava/io/PrintStream;
  #21 = NameAndType        #7:#8          // a:I
  #22 = Class              #29            // java/io/PrintStream
  #23 = NameAndType        #30:#31        // println:(I)V
  #24 = Utf8               Demo
  #25 = Utf8               java/lang/Object
  #26 = Utf8               java/lang/System
  #27 = Utf8               out
  #28 = Utf8               Ljava/io/PrintStream;
  #29 = Utf8               java/io/PrintStream
  #30 = Utf8               println
  #31 = Utf8               (I)V
{
  static int a;
    descriptor: I
    flags: ACC_STATIC

  public Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
         3: getstatic     #3                  // Field a:I
         6: invokevirtual #4                  // Method java/io/PrintStream.println:(I)V
         9: return
      LineNumberTable:
        line 10: 0
        line 11: 9

  static {};
    descriptor: ()V
    flags: ACC_STATIC
    Code:
      stack=1, locals=0, args_size=0
         0: bipush        10
         2: putstatic     #3                  // Field a:I
         5: bipush        20
         7: putstatic     #3                  // Field a:I
        10: bipush        30
        12: putstatic     #3                  // Field a:I
        15: return
      LineNumberTable:
        line 2: 0
        line 4: 5
        line 7: 10
        line 8: 15
}
SourceFile: "Demo.java"
```

## init针对非静态成员

编译器会按自上而下的顺序，收及所有的`{}`代码块（非静态代码块）和成员变量赋值代码，合并位一个特殊的`<init>()V`，按顺序执行，原始构造方法在这个新的构造方法最下面，最后执行

```java
public class Demo {
    private String a = "s1";
    {
        b=20;
    }
    private int b = 10;
    {
        a="s2";
    }
    public Demo(String a, int b) {
        this.a = a;
        this.b = b;
    }
    public static void main(String[] args) {
        Demo demo = new Demo("s3", 30);
        System.out.println(demo.a);
        System.out.println(demo.b);
    }
}
```

```java
// D:\IdeaProject\src>javap -v Demo.class
Classfile /D:/IdeaProject/src/Demo.class
  Last modified 2021-5-26; size 635 bytes
  MD5 checksum ba341881c45236116e471314616fc804
  Compiled from "Demo.java"
public class Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #12.#25        // java/lang/Object."<init>":()V
   #2 = String             #26            // s1
   #3 = Fieldref           #6.#27         // Demo.a:Ljava/lang/String;
   #4 = Fieldref           #6.#28         // Demo.b:I
   #5 = String             #29            // s2
   #6 = Class              #30            // Demo
   #7 = String             #31            // s3
   #8 = Methodref          #6.#32         // Demo."<init>":(Ljava/lang/String;I)V
   #9 = Fieldref           #33.#34        // java/lang/System.out:Ljava/io/PrintStream;
  #10 = Methodref          #35.#36        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #11 = Methodref          #35.#37        // java/io/PrintStream.println:(I)V
  #12 = Class              #38            // java/lang/Object
  #13 = Utf8               a
  #14 = Utf8               Ljava/lang/String;
  #15 = Utf8               b
  #16 = Utf8               I
  #17 = Utf8               <init>
  #18 = Utf8               (Ljava/lang/String;I)V
  #19 = Utf8               Code
  #20 = Utf8               LineNumberTable
  #21 = Utf8               main
  #22 = Utf8               ([Ljava/lang/String;)V
  #23 = Utf8               SourceFile
  #24 = Utf8               Demo.java
  #25 = NameAndType        #17:#39        // "<init>":()V
  #26 = Utf8               s1
  #27 = NameAndType        #13:#14        // a:Ljava/lang/String;
  #28 = NameAndType        #15:#16        // b:I
  #29 = Utf8               s2
  #30 = Utf8               Demo
  #31 = Utf8               s3
  #32 = NameAndType        #17:#18        // "<init>":(Ljava/lang/String;I)V
  #33 = Class              #40            // java/lang/System
  #34 = NameAndType        #41:#42        // out:Ljava/io/PrintStream;
  #35 = Class              #43            // java/io/PrintStream
  #36 = NameAndType        #44:#45        // println:(Ljava/lang/String;)V
  #37 = NameAndType        #44:#46        // println:(I)V
  #38 = Utf8               java/lang/Object
  #39 = Utf8               ()V
  #40 = Utf8               java/lang/System
  #41 = Utf8               out
  #42 = Utf8               Ljava/io/PrintStream;
  #43 = Utf8               java/io/PrintStream
  #44 = Utf8               println
  #45 = Utf8               (Ljava/lang/String;)V
  #46 = Utf8               (I)V
{
  public Demo(java.lang.String, int);
    descriptor: (Ljava/lang/String;I)V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=3, args_size=3
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: aload_0
         5: ldc           #2                  // String s1
         7: putfield      #3                  // Field a:Ljava/lang/String;
        10: aload_0
        11: bipush        20
        13: putfield      #4                  // Field b:I
        16: aload_0
        17: bipush        10
        19: putfield      #4                  // Field b:I
        22: aload_0
        23: ldc           #5                  // String s2
        25: putfield      #3                  // Field a:Ljava/lang/String;
        28: aload_0
        29: aload_1
        30: putfield      #3                  // Field a:Ljava/lang/String;
        33: aload_0
        34: iload_2
        35: putfield      #4                  // Field b:I
        38: return
      LineNumberTable:
        line 10: 0
        line 2: 4
        line 4: 10
        line 6: 16
        line 8: 22
        line 11: 28
        line 12: 33
        line 13: 38

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=2, args_size=1
         0: new           #6                  // class Demo
         3: dup
         4: ldc           #7                  // String s3
         6: bipush        30
         8: invokespecial #8                  // Method "<init>":(Ljava/lang/String;I)V
        11: astore_1
        12: getstatic     #9                  // Field java/lang/System.out:Ljava/io/PrintStream;
        15: aload_1
        16: getfield      #3                  // Field a:Ljava/lang/String;
        19: invokevirtual #10                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        22: getstatic     #9                  // Field java/lang/System.out:Ljava/io/PrintStream;
        25: aload_1
        26: getfield      #4                  // Field b:I
        29: invokevirtual #11                 // Method java/io/PrintStream.println:(I)V
        32: return
      LineNumberTable:
        line 15: 0
        line 16: 12
        line 17: 22
        line 18: 32
}
SourceFile: "Demo.java"

```

# 方法的调用

## 字节码分析

```java
public class Demo {
    public Demo() { }
    private void test1() { }
    private final void test2() { }
    public void test3() { }
    public static void test4() { }
    public static void main(String[] args) {
        Demo d = new Demo();
        d.test1();
        d.test2();
        d.test3();
        d.test4();
        Demo.test4();
    }
}
```

```java
D:\IdeaProject\src>javap -v Demo.class
Classfile /D:/IdeaProject/src/Demo.class
  Last modified 2021-5-26; size 538 bytes
  MD5 checksum 16b183ab804846ad0fa93d65659115fa
  Compiled from "Demo.java"
public class Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #8.#21         // java/lang/Object."<init>":()V
   #2 = Class              #22            // Demo
   #3 = Methodref          #2.#21         // Demo."<init>":()V
   #4 = Methodref          #2.#23         // Demo.test1:()V
   #5 = Methodref          #2.#24         // Demo.test2:()V
   #6 = Methodref          #2.#25         // Demo.test3:()V
   #7 = Methodref          #2.#26         // Demo.test4:()V
   #8 = Class              #27            // java/lang/Object
   #9 = Utf8               <init>
  #10 = Utf8               ()V
  #11 = Utf8               Code
  #12 = Utf8               LineNumberTable
  #13 = Utf8               test1
  #14 = Utf8               test2
  #15 = Utf8               test3
  #16 = Utf8               test4
  #17 = Utf8               main
  #18 = Utf8               ([Ljava/lang/String;)V
  #19 = Utf8               SourceFile
  #20 = Utf8               Demo.java
  #21 = NameAndType        #9:#10         // "<init>":()V
  #22 = Utf8               Demo
  #23 = NameAndType        #13:#10        // test1:()V
  #24 = NameAndType        #14:#10        // test2:()V
  #25 = NameAndType        #15:#10        // test3:()V
  #26 = NameAndType        #16:#10        // test4:()V
  #27 = Utf8               java/lang/Object
{
  public Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 2: 0

  public void test3();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=0, locals=1, args_size=1
         0: return
      LineNumberTable:
        line 5: 0

  public static void test4();
    descriptor: ()V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=0, locals=0, args_size=0
         0: return
      LineNumberTable:
        line 6: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=1
         0: new           #2                  // class Demo
         3: dup
         4: invokespecial #3                  // Method "<init>":()V
         7: astore_1
         8: aload_1
         9: invokespecial #4                  // Method test1:()V
        12: aload_1
        13: invokespecial #5                  // Method test2:()V
        16: aload_1
        17: invokevirtual #6                  // Method test3:()V
        20: aload_1
        21: pop
        22: invokestatic  #7                  // Method test4:()V
        25: invokestatic  #7                  // Method test4:()V
        28: return
      LineNumberTable:
        line 8: 0
        line 9: 8
        line 10: 12
        line 11: 16
        line 12: 20
        line 13: 25
        line 14: 28
}
SourceFile: "Demo.java"
```

## 多态的原理(invokeVirtua调用原理)

1. 虚方法表是在类的加载的链接阶段生成的，并且确定每个虚方法的入口地址
2. 子类重写父类的方法，方法地址存在虚方法表中
3. 执行流程：当执行`invokevirtual`指令时
   1. 先通过栈帧中的对象引用找到对象
   2. 分析对象头，赛到对象的实际`Class`
   3.  `Class` 结构中有 `vtable` ,它在类加载的链接阶段就已经根据方法的重写规则生成好了
   4. 查表得到方法的具体地址
   5. 执行方法的字节码

# 异常的处理

## 异常字节码分析

注意：`finally`的工作方式底层是把代码放到`try`分支和`catch`分支后面这样保证`finally`一定执行

可以看到多出来一个`Exception table`的结构，`[from, to)`检测范围，一旦这个范围内的字节码执行出现异常，则通过`type`匹配异常类型，如果一致，进入`target`所指示行号11行的字节码指令`astoi_e_2`是将异常对象弓|用存入局部变量表的`slot 2`位置注意：`finally`的工作方式底层是把代码放到`try`分支和`catch`分支后面这样保证`finally`一定执行

```java
public class Demo {
    public static void main(String[] args) {
        int i = 0;
        try {
            i = 10;
        } catch (Exception e) {
            i = 20;
        }finally{
            i=30;
        }
    }
}
```

```java
// D:\IdeaProject\src>javap -v Demo.class
Classfile /D:/IdeaProject/src/Demo.class
  Last modified 2021-5-26; size 465 bytes
  MD5 checksum b4140fe637e2f9e51eb63e32f88ac267
  Compiled from "Demo.java"
public class Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #4.#17         // java/lang/Object."<init>":()V
   #2 = Class              #18            // java/lang/Exception
   #3 = Class              #19            // Demo
   #4 = Class              #20            // java/lang/Object
   #5 = Utf8               <init>
   #6 = Utf8               ()V
   #7 = Utf8               Code
   #8 = Utf8               LineNumberTable
   #9 = Utf8               main
  #10 = Utf8               ([Ljava/lang/String;)V
  #11 = Utf8               StackMapTable
  #12 = Class              #21            // "[Ljava/lang/String;"
  #13 = Class              #18            // java/lang/Exception
  #14 = Class              #22            // java/lang/Throwable
  #15 = Utf8               SourceFile
  #16 = Utf8               Demo.java
  #17 = NameAndType        #5:#6          // "<init>":()V
  #18 = Utf8               java/lang/Exception
  #19 = Utf8               Demo
  #20 = Utf8               java/lang/Object
  #21 = Utf8               [Ljava/lang/String;
  #22 = Utf8               java/lang/Throwable
{
  public Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=4, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10     // try
         4: istore_1
         5: bipush        30     // finally
         7: istore_1
         8: goto          27     // return
        11: astore_2			// catch Exceptin
        12: bipush        20
        14: istore_1
        15: bipush        30     // finally
        17: istore_1
        18: goto          27	// return
        21: astore_3	        // catch any -> slot 3
        22: bipush        30	// finally
        24: istore_1
        25: aload_3
        26: athrow			   // throw
        27: return
      Exception table:
         from    to  target type
             2     5    11   Class java/lang/Exception
             2     5    21   any   // 剩余的异常类型, 比如Error 
            11    15    21   any   // 剩余的异常类型，比如Error
      LineNumberTable:
        line 3: 0
        line 5: 2
        line 9: 5
        line 10: 8
        line 6: 11
        line 7: 12
        line 9: 15
        line 10: 18
        line 9: 21
        line 10: 25
        line 11: 27
      StackMapTable: number_of_entries = 3
        frame_type = 255 /* full_frame */
          offset_delta = 11
          locals = [ class "[Ljava/lang/String;", int ]
          stack = [ class java/lang/Exception ]
        frame_type = 73 /* same_locals_1_stack_item */
          stack = [ class java/lang/Throwable ]
        frame_type = 5 /* same */
}
SourceFile: "Demo.java"
```

# finally的返回值

```java
// 面试题1：finally对return的影响，return会暂存起来
public class HelloWorld {
    public static void main(String[] args) {
      int a=test();
        System.out.println(a);//30
    }
    public static int test() {
        int i=0;
        try {
            i=10;
            return i;
        } catch (Exception e) {
            i=20;
            return i;
        } finally {
            i=30; // finally中执行return，会吞掉异常，看不到异常信息
            return i; // 如果这里没有return,那么返回结果就是10了
        }
    }
}
// 注意：finally一定会执行，所以返回值是30
```

# synchornized出现异常仍能解锁

```java
public class Demo {
    public static void main(String[] args) {
        Object lock = new Object();
        synchronized (lock){
            System.out.println("ok");
        }
    }
}
```

```java
// D:\IdeaProject\src>javap -v Demo.class
Classfile /D:/IdeaProject/src/Demo.class
  Last modified 2021-5-26; size 553 bytes
  MD5 checksum 66c42ec683ef19325c88875349e345cf
  Compiled from "Demo.java"
public class Demo
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #2.#19         // java/lang/Object."<init>":()V
   #2 = Class              #20            // java/lang/Object
   #3 = Fieldref           #21.#22        // java/lang/System.out:Ljava/io/PrintStream;
   #4 = String             #23            // ok
   #5 = Methodref          #24.#25        // java/io/PrintStream.println:(Ljava/lang/String;)V
   #6 = Class              #26            // Demo
   #7 = Utf8               <init>
   #8 = Utf8               ()V
   #9 = Utf8               Code
  #10 = Utf8               LineNumberTable
  #11 = Utf8               main
  #12 = Utf8               ([Ljava/lang/String;)V
  #13 = Utf8               StackMapTable
  #14 = Class              #27            // "[Ljava/lang/String;"
  #15 = Class              #20            // java/lang/Object
  #16 = Class              #28            // java/lang/Throwable
  #17 = Utf8               SourceFile
  #18 = Utf8               Demo.java
  #19 = NameAndType        #7:#8          // "<init>":()V
  #20 = Utf8               java/lang/Object
  #21 = Class              #29            // java/lang/System
  #22 = NameAndType        #30:#31        // out:Ljava/io/PrintStream;
  #23 = Utf8               ok
  #24 = Class              #32            // java/io/PrintStream
  #25 = NameAndType        #33:#34        // println:(Ljava/lang/String;)V
  #26 = Utf8               Demo
  #27 = Utf8               [Ljava/lang/String;
  #28 = Utf8               java/lang/Throwable
  #29 = Utf8               java/lang/System
  #30 = Utf8               out
  #31 = Utf8               Ljava/io/PrintStream;
  #32 = Utf8               java/io/PrintStream
  #33 = Utf8               println
  #34 = Utf8               (Ljava/lang/String;)V
{
  public Demo();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         0: new           #2                  // new Object class java/lang/Object
         3: dup							    // 复制，一个消耗操作数栈顶的构造方法，一个存入局部变量表
         4: invokespecial #1                  // Method java/lang/Object."<init>":()V
         7: astore_1						// 1ock引用->1ock
         8: aload_1							// 1ock( synchronized开始)
         9: dup								// 复制，一个给加锁对象使用，一个给解锁使用
        10: astore_2						// 1ock引用->slot2
        11: monitorenter					// moitorenter(消耗1ock引用，加锁操作)
        12: getstatic     #3                  // Field java/lang/System.out:Ljava/io/PrintStream;
        15: ldc           #4                  // String ok
        17: invokevirtual #5                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        20: aload_2							// s1ot2(1ock引用)
        21: monitorexit						// monitorexit(消耗1ock引用，解锁操作)
        22: goto          30
        25: astore_3						// any -> slot 3
        26: aload_2							// s1ot2(1ock引用，即使出现异常，也会解锁)
        27: monitorexit						// monitorexit(1ock引用，出现异常之后解锁)（异常处理）
        28: aload_3
        29: athrow
        30: return
      Exception table:
         from    to  target type
            12    22    25   any
            25    28    25   any
      LineNumberTable:
        line 3: 0
        line 4: 8
        line 5: 12
        line 6: 20
        line 7: 30
      StackMapTable: number_of_entries = 2
        frame_type = 255 /* full_frame */
          offset_delta = 25
          locals = [ class "[Ljava/lang/String;", class java/lang/Object, class java/lang/Object ]
          stack = [ class java/lang/Throwable ]
        frame_type = 250 /* chop */
          offset_delta = 4
}
SourceFile: "Demo.java"
```

# 语法糖

## 构造方法

```java
public HelloWorld() {}
// 相当于下面
public HelloWorld() {
    // 调用object父类的无参构造
    super();
}
```

## 自动拆箱装箱(略)

## 泛型擦除

1. `JAVA`代码中泛型，在编译成字节码之后，已经不区分类型了，都是`Object`类型(这就是泛型擦除)
2. 比如list集合中存的是`Integer`，`get()`获得其实是`Object`类型，强制类型转换为`Integer`
3. 泛型擦除的是方法体中的泛型，**局部变量类型表**中还包含泛型信息

## 可变参数

`String...args`其实是`string[] args`

## foreach

1. 数组：增强 `for` 循环，本质上就是用下标遍历的循环
2. 集合：获取 `List` 的`iterator`，判断迭代器是否有下一个元素。`iterator.next() ` 获取下一个元素
3. 只有实现 `Iterable` 接口才能配合 `foreach` 实现遍历

## switch

1. 配合 `String`：实质是两个 `switch`
   1. 第一遍 `switch` 利用 `hashcode`（保证效率）来比较，由于可能哈希冲突（ `hash` 值相同字符串不同），所以需要在 `case` 内部使用 `equals`，返回临时变量 `x`
   2. 第二遍利用 `x` 来做 `switch` 判断
2. 配合 `Enum`：同样经过一层转换，然后在做 `switch` 判断

## 枚举

`Enum  ` 内部成员其实是实例对象

## try with resource

1. 语法：`try（资源变量=资源对象）{ }catch{ }`
2. 传统使用 `try-catch-finally`，需要在 `finally` 释放资源，如果 `try` 声明资源，就不用 `finally` 自己释放资源类
3. 要求：资源对象必须实现 `AutoCloseable` 接口，如`InputStream`，`OutputStream`
4. 编译器自动帮助生成 `finally` 关闭资源
5. 不仅 `try` 过程的异常保留下来，并且资源回收过程异常也保留下来(**压制异常**)

## 方法重写的桥接方法

1. 重写发生在以下两种情况
2. 父子类方法返回值一致
3. 子类方法返回值可以是子类型
   1. 编译器会自动生成返回值是父类型的方法(这个是桥接方法)
   2. 并且在这个方法内部，调用我们重写的返回值是子类型的方法

## 匿名内部类

1. 相当于创建一个额外的类，类名一般是外部类名+$+一个数字
2. 外部类就相当于创建了内部类的实例
3. 匿名内部类引用外部变量，要求变量是`final`修饰。此时匿名内部类(外部变量当作自己成员变量，所以需要用`final`修饰)

# 类加载阶段

## 加载

1. 将类的字节码载入方法区，内部采用 `c++` 的 `instanceKlass` 描述 `JAVA` 类
2. `*`生成 `java.lang.Class` 对象是在加载时进行的。生成 `Class` 对象作为方法区这个类的各种数据的访问入口

![批注 2020-04-04 233746](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-233746.png)

![批注 2020-04-04 233841](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-04-233841.png)

## 连接

1. **验证：验证类是否符合JVM规范，安全性检查**
2. 准备：**为静态变量分配空间**，设置默认值（静态变量存储在堆中）
   1. 分配空间和赋值是两个步骤，分配空间在准备阶段完成，赋值在初始化阶段完成
   2. 如果 `static` 变量是 `final` 修饰的基本类型（引用类型），那么赋值在准备阶段（初始化阶段）进行
3. 解析：**将常量池中的符号引用解析为直接引用**（知道引用的类，方法在内存中的地址），这个阶段可能导致类加载

## 初始化

1. 其实就是调用`<cinit>()V`方法（`static{}`代码块此时执行），虚拟机会保证这个类的构造方法线程安全
2. 发生时机
   1. 概括地说类的初始化是懒惰的
   2. `main`方法所在的类，总是先被初始化
   3. `new` 关键字导致初始化
   4. `Class.forName()`
   5. 首次访问这个类的静态变量和静态方法时
   6. 子类初始化，父类还未初始化，会引发
   7. 子类访问父类的静态变量，只会触发父类的初始化，子类的加载
3. 不导致类初始化的情况
   1. 访问类的 `static final` 静态变量(基本类型，字符串)不会触发（准备阶段就完成了）
   2. `类对象.class`不会触发初始化（加载阶段）
   3. 创建该类的数组不会初始化
   4. 类加载的 `loadClass()` 方法
   5. `Class.forName()`第二个参数为`false`时

# 类加载器(四种)

## 四种类加载器

1. `Bootstrap ClassLoader`(启动类加载器)
   1. 只加载`JAVA_HOME/jre/lib`下的类
   2. 无法直接访问，显示为`null`
2. `Extension ClassLoader`(扩展加载器)
   1. 只加载`JAVA_HOME/jre/lib/ext`下的类
   2. 上级为`Bootstrap`，但是只能拿到`null`
3. `Application ClassLoader`(应用加载器)
   1. 加载`classpath`下的类
   2. 上级为`Extension`
4. 线程上下文类加载器：拿到的其实`App`加载器
   1. 原理：线程启动时`JVM`把应用类加载器（默认）赋值给当前线程
   2. 破坏了双亲委派机制，类加载只交由线程上下文类加载器执行。调用的是`loadClass()`方法不能保证双亲委派！
5. 自定义类加载器
   1. 自定义加载哪些类，上级为`Application`
   2. 重写`findclass()`，`findClass()`中调用defineClass()方法加载`Class`

## 双亲委派

1. 定义：所谓双亲委派，其实是类加载器调用`findClass()`方法
2. 原理：当某个类加载器需要加载某个`.class`文件时，它首先把这个任务委托给他的上级类加载器，递归这个操作(上级再委托自己的上级)，如果上级类加载器没有加载，自己才会去加载这个类
3. 作用：
   1. 防止重复加载一个类，通过委托询问上级是否加载过，保证数据安全
   2. 保证核心`.class`不能被篡改，通过委托方式，不会去篡改核心`.class`，即使篡改也不会去加载，即使加载也不会是同一个`.class`对象了。不同的加载器加载同一个`.class`也不是同一个 `Class` 对象。这样保证了`Class`执行安全
   3. 这种设计有个好处是，如果有人想替换系统级别的类：`String.java`。篡改它的实现，在这种机制下这些系统的类已经被`Bootstrap classLoader`加载过了（为什么？因为当一个类需要加载的时候，最先去尝试加载的就是`BootstrapClassLoader`），所以其他类加载器并没有机会再去加载，从一定程度上防止了危险代码的植入

# 运行期优化

## 逃逸分析

![批注 2020-04-05 152024](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-05-152024.png)

## 方法内联

![批注 2020-04-05 153916](https://img-1301279461.cos.ap-nanjing.myqcloud.com/img/%E6%89%B9%E6%B3%A8-2020-04-05-153916.png)

## 字段优化

尽量使用局部变量(通过局部变量使用成员变量的数据)，操作局部变量提高性能

## 反射优化

1. 当通过反射调用一个方法（使用的是本地方法`native`，效率比较低），每调用一次，膨胀值+1，膨胀阈值15（可以设置）
2. 当调用到15次是`JVM`采用另外一种方式调用方法（其实就是正常的方法而不是反射调用）